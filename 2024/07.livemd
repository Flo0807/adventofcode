# Advent of Code 2024 - Day 07

```elixir
Mix.install([
  {:req, "~> 0.5"},
  {:benchee, "~> 1.3"}
])
```

## Input

```elixir
opts = [headers: [{"cookie", "session=#{System.fetch_env!("LB_AOC_SESSION")}"}]]
puzzle_input = Req.get!("https://adventofcode.com/2024/day/7/input", opts).body
```

```elixir
puzzle_input =
  String.split(puzzle_input, "\n", trim: true)
  |> Enum.map(fn row ->
    [sum, nums] = String.split(row, ":", trim: true)

    sum = String.to_integer(sum)
    nums = String.split(nums, " ", trim: true) |> Enum.map(&String.to_integer(&1))

    {sum, nums}
  end)
```

```elixir
defmodule BridgeRepair do
  def sum_of_sums(rows, operators) do
    Task.async_stream(rows, fn {sum, nums} ->
      calculate_sums(nums, operators) |> Enum.find(0, &(&1 == sum))
    end)
    |> Enum.reduce(0, fn {:ok, x}, acc -> x + acc end)
  end

  def calculate_sums(nums, operators) do
    [head | tail] = nums

    Enum.reduce(tail, [head], fn num, acc ->
      for operator <- operators do
        case operator do
          :multiply -> Enum.map(acc, &(&1 * num))
          :add -> Enum.map(acc, &(&1 + num))
          :concatenation -> Enum.map(acc, &concat_nums(&1, num))
        end
      end
      |> List.flatten()
    end)
  end

  def concat_nums(a, b) do
    a * Integer.pow(10, Integer.digits(b) |> length) + b
  end
end
```

## Puzzle 1

```elixir
puzzle_1 = fn ->
  operators = [:multiply, :add]
  BridgeRepair.sum_of_sums(puzzle_input, operators)
end

puzzle_1.()
```

## Puzzle 2

```elixir
puzzle_2 = fn ->
  operators = [:multiply, :add, :concatenation]
  BridgeRepair.sum_of_sums(puzzle_input, operators)
end

puzzle_2.()
```

## Benchmarks

```elixir
Benchee.run(
  %{
    "puzzle_1" => fn -> puzzle_1.() end,
    "puzzle_2" => fn -> puzzle_2.() end
  })
```

| Name     | ips   | average   | deviation | median    | 99th %    |
| -------- | ----- | --------- | --------- | --------- | --------- |
| puzzle_1 | 89.17 | 11.22 ms  | ±10.55%  | 11.05 ms  | 17.76 ms  |
| puzzle_2 | 5.27  | 189.76 ms | ±1.38%   | 189.17 ms | 201.39 ms |

<!-- livebook:{"offset":2058,"stamp":{"token":"XCP.w9PZg1Nzfw65NvXyONcjOxYE0rXhUhuZZ31-MYmn4WuIIZI4QuxE9FmqThrILYdRcqbUyt5d0uTzt7qWXtXAfLuWKq0WbIfQwT60sU-Y5wjKsEXcW0M","version":2}} -->
